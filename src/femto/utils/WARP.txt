DVAR $thisX $thisY $thisZ
DVAR $margin $meshSizeX $meshSizeY
DVAR $angle
DVAR $sizeX $sizeY
DVAR $pointsX $pointsY
DVAR $iii $jjj

<HEADER>

; +--- PROGRAM DATA ---+
; CREATE MESH FOR COMPENSATION OF CHIP WARP
$sizeX = {sizeX:.6f}		; [mm] size in X of the chip
$sizeY = {sizeY:.6f}		; [mm] size in Y of the chip
$margin = {margin:.6f}		; [mm] distance of the mesh from the chip border
$pointsX = {ptsX}			; Number of points of the mesh per row on X
$pointsY = {ptsY}			; Number of points of the mesh per column on Y
$angle = {angle:.6f}		; [deg] AeroTech rotation angle

$meshSizeX = $sizeX - 2*$margin
$meshSizeY = $sizeY - 2*$margin


; CLEAR ALL CONSOLE MESSAGES, CLOSE ALL OPEN FILE HANDLES AND ADD TEXT HEADER
MSGCLEAR -1
FILECLOSE
FILEWRITE "POS.txt", "    X     ", "     Y     ", "     Z"


; +--- START SAMPLING SCRIPT ---+
LINEAR X0.000000 Y0.000000 Z0.000000 F5.000000
G84 X Y
DWELL 0.05
G84 X Y F$angle

LINEAR X$margin Y$margin
PSOCONTROL X ON
DWELL 0.1

FOR $iii = 0 to $pointsX-1
	$thisX = $margin + $iii * $meshSizeX / ($pointsX-1)
	LINEAR X $thisX F20.000000
	DWELL 0.1

	FOR $jjj = 0 to $pointsY-1
		; MOVE ALONG
		$thisY = $margin + $jjj * $meshSizeY / ($pointsY-1)
		LINEAR Y $thisY F20.000000

		; POP UP MESSAGE AND SAVE DATA
		MSGLAMP 1, CYAN, "Metti bene a fuoco."
		MSGLAMP 2, GREEN, "MI RACCOMANDO."

		PROGRAM 1 STEPINTO
		$thisZ = AXISSTATUS(Z,DATAITEM_ProgramPositionCommand)

		; WRITE DATA ON FILE
		FILEWRITE "POS.txt", $thisX, " ", $thisY, " ", $thisZ

		MSGLAMP 1, GRAY, ""
		MSGLAMP 2, GRAY, ""
		DWELL 0.2
	NEXT $jjj

	; RETURN TO STARTING Y POSITION
	LINEAR Y$margin F20.000000
	DWELL 0.1
NEXT $iii

; RETURN TO STARTING X POSITION
LINEAR X$margin F20.000000
PSOCONTROL X OFF
DWELL 0.1

LINEAR X0.000000 Y0.000000 Z0.000000 F5.000000
G84 X Y

END PROGRAM
